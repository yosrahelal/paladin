plugins {
    id 'java'
    id 'com.google.protobuf' version '0.9.4'
}

group = 'io.kaleido'

repositories {
    mavenCentral()
}

test {
    testLogging {
        showStandardStreams = true
    }
    useJUnitPlatform()
}

configurations {
    // Resolvable configurations
    toolkitProtoDefinitions {
        canBeConsumed = false
        canBeResolved = true
    }
}

tasks.register('collectProtocFiles', Copy) {
    from configurations.toolkitProtoDefinitions.asFileTree
    into "src/main/proto"
}

tasks.register('cleanProto', Delete) {
    delete 'src/main/proto'
}

tasks.named('generateProto') {
    dependsOn 'collectProtocFiles'
}

tasks.named('processResources') {
    dependsOn 'generateProto'
}

tasks.named('clean') {
    dependsOn tasks.cleanProto
}

tasks.register("protoc") {
    dependsOn tasks.generateProto
}

def grpcVersion = '1.65+'
def protobufVersion = '3.25+'
def protocVersion = protobufVersion

dependencies {
    // GRPC and Protobuf dependencies
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-services:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation "io.grpc:grpc-netty:${grpcVersion}"

    implementation 'org.bouncycastle:bcprov-jdk18on:1.78+'
    implementation 'io.netty:netty-all:4.1+'
    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.17+'

    implementation 'org.apache.logging.log4j:log4j-core:2.23+'

    toolkitProtoDefinitions project(path: ":toolkit:proto", configuration: "protoDefinitions")

    implementation 'net.java.dev.jna:jna-platform:5.14+'
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"

    compileOnly "org.apache.tomcat:annotations-api:6.0+"

    // Test dependencies
    testImplementation project(path: ":solidity", configuration: "compiledContracts")
    testImplementation platform('org.junit:junit-bom:5.9+')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.skyscreamer:jsonassert:1.5.3'
}

protobuf {
    protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
    plugins {
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
    }
    generateProtoTasks {
        all()*.plugins { grpc {} }
    }
}

tasks.named('assemble') {
    // this package is not intended to be built independently
    enabled = false
}