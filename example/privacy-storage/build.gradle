/*
 * Copyright Â© 2024 Kaleido, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

configurations {
    // Resolvable configurations
    contractCompile {
        canBeConsumed = false
        canBeResolved = true
    }
    buildSDK {
        canBeConsumed = false
        canBeResolved = true
    }
    buildCommon {
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    contractCompile project(path: ':solidity', configuration: 'compiledContracts')
    buildSDK project(path: ':sdk:typescript', configuration: 'buildSDK')
    buildCommon project(path: ':example:common', configuration: 'buildCommon')
}

task install(type: Exec) {
    executable 'npm'
    args 'install'

    inputs.files(configurations.buildSDK)
    inputs.files(configurations.buildCommon)
    inputs.files('package.json')
    outputs.files('package-lock.json')
    outputs.dir('node_modules')
}

task copyABI(type: Exec, dependsOn: install) {
    executable 'npm'
    args 'run'
    args 'abi'

    inputs.files(configurations.contractCompile)
    inputs.dir('scripts')
    outputs.dir('src/abis')
}

task build(type: Exec, dependsOn: [install, copyABI]) {
    executable 'npm'
    args 'run'
    args 'build'

    inputs.dir('src')
    outputs.dir('build')
}

task e2e(type: Exec, dependsOn: [build]) {
    dependsOn ':operator:deploy'

    executable 'npm'
    args 'run'
    args 'start'
}

task clean(type: Delete) {
    delete 'node_modules'
    delete 'build'
}
