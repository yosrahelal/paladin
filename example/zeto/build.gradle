/*
 * Copyright Â© 2024 Kaleido, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License"); you may not use this file except in compliance with
 * the License. You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software distributed under the License is distributed on
 * an "AS IS" BASIS, WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied. See the License for the
 * specific language governing permissions and limitations under the License.
 *
 * SPDX-License-Identifier: Apache-2.0
 */

configurations {
    // Resolvable configurations
    buildSDK {
        canBeConsumed = false
        canBeResolved = true
    }
    buildCommon {
        canBeConsumed = false
        canBeResolved = true
    }
}

dependencies {
    buildSDK project(path: ':sdk:typescript', configuration: 'buildSDK')
    buildCommon project(path: ':example:common', configuration: 'buildCommon')
}

task copyContracts(type: Copy, dependsOn: [":domains:zeto:extractZetoArtifacts"]) {
    from fileTree("../../domains/zeto/zkp/artifacts/contracts") {
        include '**/SampleERC20.json'
    }
    into './src/abis'

    // Flatten all paths into the destination folder
    eachFile { path = name }
    includeEmptyDirs = false
}

task install(type: Exec) {
    executable 'npm'
    args 'install'

    inputs.files(configurations.buildSDK)
    inputs.files(configurations.buildCommon)
    inputs.files('package.json')
    outputs.files('package-lock.json')
    outputs.dir('node_modules')
}

task build(type: Exec, dependsOn: [install, copyContracts]) {
    executable 'npm'
    args 'run'
    args 'build'

    inputs.dir('src')
    outputs.dir('build')
}

task e2e(type: Exec, dependsOn: [build]) {
    dependsOn ':operator:e2e'

    executable 'npm'
    args 'run'
    args 'start'
}

task clean(type: Delete) {
    delete 'node_modules'
    delete 'build'
}
